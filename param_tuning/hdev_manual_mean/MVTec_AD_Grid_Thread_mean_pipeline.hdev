<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<l>dataset_name := 'MVTec_AD_Grid_Thread_mean_pipeline'</l>
<l>source_path := 'I:/evias_expmts/MVTecAnomalyDetection/grid_thread_train/images'</l>
<l>labels_path := 'I:/evias_expmts/MVTecAnomalyDetection/grid_thread_train/labels'</l>
<l>out_path := 'D:/dev/experiments/param_tuning/hdev_manual_mean/' + dataset_name + '/'</l>
<c></c>
<l>dev_update_off()</l>
<l>list_image_files(source_path, 'default', [], ImageFiles)</l>
<l>list_image_files(labels_path, 'default', [], LabelFiles)</l>
<c></c>
<l>file_exists(out_path, dir_exists)</l>
<c></c>
<l>if(dir_exists == 0)</l>
<l>    make_dir(out_path)</l>
<l>else</l>
<c>    * Delete all files in output path if folder exists</c>
<l>    list_files(out_path, 'files', FileList)</l>
<l>    for j := 0 to |FileList| - 1 by 1</l>
<l>        delete_file(FileList[j])</l>
<l>    endfor</l>
<l>endif</l>
<c></c>
<l>TotalMCC := 0.0</l>
<c></c>
<l>for Index := 1 to |ImageFiles| - 1 by 1</l>
<c>    * Remove the extension using regex</c>
<l>    tuple_split (ImageFiles[Index], '/', filepath)</l>
<l>    filename_with_extension := filepath[|filepath| - 1]</l>
<l>    tuple_regexp_replace (filename_with_extension, '\\.[a-zA-Z0-9]+$', '', filename)</l>
<c></c>
<l>    out_img_path := out_path + filename + '.png'</l>
<c>    </c>
<l>    read_image(Image, ImageFiles[Index])</l>
<l>    read_image(Label, LabelFiles[Index])</l>
<c>    </c>
<l>    get_image_size(Image, Width, Height)</l>
<c>    * --------------</c>
<c>    * CGP Code Block</c>
<c>    * --------------</c>
<l>    try</l>
<l>        MinGray := 19</l>
<l>        MaxGray := 255</l>
<c>        </c>
<l>        MinRatio := 0.0399999991059303</l>
<l>        MaskHeight := 13</l>
<l>        MaskWidth := 19</l>
<c>        </c>
<l>        FilterType := 'y'</l>
<l>        MaskSize := 7      </l>
<c>             </c>
<l>        Sigma := 0.5</l>
<c>        </c>
<l>        A_1 := 21</l>
<l>        B_1 := 20</l>
<c>        </c>
<l>        A_2 := 27</l>
<l>        B_2 := 7</l>
<l>        C_2 := 0.392699</l>
<c>        </c>
<c>        * CropSmallesRectangle</c>
<c>        * 1 [label="CropSmallestRectangle\nMinGray=19\n MaxGray=255"];</c>
<c></c>
<l>        threshold(Image, Region, MinGray, MaxGray)</l>
<l>        smallest_rectangle1(Region, Row1, Column1, Row2, Column2)</l>
<l>        crop_rectangle1(Image, Image, Row1, Column1, Row2, Column2)</l>
<c>        </c>
<l>        copy_image(Image, Image1)</l>
<l>        copy_image(Image, Image2)</l>
<c>        </c>
<c>        * Path 1</c>
<c>        * -------</c>
<c>        * CropRectangle</c>
<c>        * 20 [label="CropRectangle\nMinRatio=0,0399999991059303\n MaskHeight=13\n MaskWidth=19"];</c>
<c>        </c>
<l>        tuple_real(MinRatio, MinRatio)</l>
<l>        gen_empty_region(Region1)</l>
<c>        </c>
<l>        count_channels(Image1, NumChannels)</l>
<l>        if(NumChannels &gt; 1)</l>
<l>            access_channel(Image1, Image1, 1)          </l>
<l>        endif</l>
<c>        </c>
<l>        get_image_type(Image1, Type)</l>
<l>        if(Type != 'byte' and Type != 'uint2' and Type != 'direction' and Type != 'cyclic' and Type != 'real')</l>
<l>            convert_image_type(Image1, Image1, 'byte')</l>
<l>        endif</l>
<l>        fast_threshold(Image1, Region1, 45, 255, 80)</l>
<l>        fill_up(Region1, Rectangle)</l>
<c>        </c>
<l>        gen_empty_obj(FaultyRegion)</l>
<l>        gen_empty_obj(NewImgReduced)</l>
<l>        gen_empty_obj(ImgPart)</l>
<l>        gen_empty_obj(RelativeRegion)</l>
<c>        </c>
<l>        smallest_rectangle1(Rectangle, Row1, Col1, Row2, Col2)</l>
<l>        reduce_domain(Image1, Rectangle, NewImageReduced)</l>
<l>        region_features(Rectangle, 'width', RegWidth)</l>
<l>        region_features(Rectangle, 'height', RegHeight)</l>
<c>        </c>
<l>        WStep := RegWidth / MaskWidth</l>
<l>        HStep := RegHeight / MaskHeight</l>
<c>        </c>
<l>        EndW := (Col2 - (WStep / 1.5)) - 20</l>
<l>        StepW := WStep / 2</l>
<c>        </c>
<l>        for ImgWidth := Col1 + 20 to EndW by StepW</l>
<l>            EndH := Row2 - (HStep / 1.5)            </l>
<l>            StepH := HStep / 2</l>
<c>            </c>
<l>            if(StepW == 0.0)</l>
<l>                break</l>
<l>            endif</l>
<c>            </c>
<l>            for ImgHeight := Row1 + 3 to EndH by StepH</l>
<l>                if(StepH == 0.0)</l>
<l>                    break</l>
<l>                endif</l>
<l>                gen_empty_obj(ImagePart)</l>
<l>                crop_rectangle1(NewImageReduced, ImgPart, ImgHeight, ImgWidth, ImgHeight + HStep, ImgWidth + WStep)</l>
<c>                </c>
<l>                gray_histo_range(ImgPart, ImgPart, 0, 255, 2, Histo, BinSize)</l>
<c>                </c>
<l>                PixelCount := Histo[0] + Histo[1]</l>
<c>                </c>
<l>                tuple_real(PixelCount, PixelCount)</l>
<c>                </c>
<l>                if(PixelCount &lt;= 0.6 * WStep * HStep)</l>
<l>                    continue</l>
<l>                endif</l>
<l>                Ratio := (Histo[1] * 1.0) / (PixelCount * 1.0)</l>
<c>                </c>
<l>                if(Ratio &lt; MinRatio)</l>
<l>                    gen_rectangle1(FaultyRegion, ImgHeight, ImgWidth, ImgHeight + HStep, ImgWidth + WStep)</l>
<l>                    concat_obj(FaultyRegion, RelativeRegion, FaultyRegion)</l>
<l>                    union1(FaultyRegion, RelativeRegion)</l>
<l>                endif</l>
<l>            endfor</l>
<l>        endfor</l>
<c></c>
<l>        count_obj(RelativeRegion, Number)</l>
<l>        if(Number == 0)</l>
<l>            gen_empty_region(Region1)</l>
<l>        else</l>
<l>            Region1 := RelativeRegion</l>
<l>        endif</l>
<c>        </c>
<c>        * Path 2</c>
<c>        * ------</c>
<c>        * SobelAmp  </c>
<c>        * 19 [label="SobelAmp\nFilterType=y\n MaskSize=7"];</c>
<l>        sobel_amp(Image, Image2, FilterType, MaskSize)</l>
<c>        </c>
<c>        * AutoThreshold</c>
<c>        * 22 [label="AutoThreshold\nSigma=0,5"];           </c>
<l>        abs_image(Image2, Image2)</l>
<l>        if(Type != 'byte' and Type != 'uint2' and Type != 'real')</l>
<l>            convert_image_type(Image2, Image2, 'byte')</l>
<l>        endif</l>
<l>        auto_threshold(Image2, Region2, Sigma)</l>
<l>        gen_empty_region(Region2)</l>
<c>        </c>
<c>        * Union2 (Path 1 &gt; Path 2)</c>
<c>        * 36 [label="Union2\n"];</c>
<l>        union2(Region1, Region2, Region)</l>
<c></c>
<c>        * MERGE 1/2</c>
<c>        * ------</c>
<c>        </c>
<c>        * Opening        </c>
<c>        * 69 [label="Opening\nStructElement=Ellipse\n A=21\n B=20"];</c>
<c>  </c>
<c>        * StructElementType Ellipse</c>
<c>        * using A, B and C as shape_params</c>
<l>        tuple_max2(A_1, B_1, max_rad)</l>
<l>        longer := A_1</l>
<l>        shorter := B_1            </l>
<l>        if (shorter &gt; longer)</l>
<l>            tmp := shorter</l>
<l>            shorter := longer</l>
<l>                    longer := tmp</l>
<l>        endif                </l>
<l>        phi := 0            </l>
<l>        tuple_ceil(max_rad + 1, max_rad_ceil)            </l>
<l>        gen_ellipse(EllipseStructElement, max_rad_ceil, max_rad_ceil, phi, longer, shorter)</l>
<l>        opening(Region, EllipseStructElement, Region)</l>
<c>                </c>
<c>        * Union1</c>
<c>        * 78 [label="Union1\n"];</c>
<l>        union1(Region, Region)</l>
<c>        </c>
<c>        *Closing</c>
<c>        * 91 [label="Closing\nA=27\n B=7\n C=0,392699\n StructElementType=Rectangle"];</c>
<c>        </c>
<l>        gen_rectangle1(RectangleStructElement, 0, 0, A_2, B_2)</l>
<l>        closing(Region, RectangleStructElement, Region) </l>
<l>    catch (Exception)</l>
<l>        gen_empty_region(Region)</l>
<l>    endtry</l>
<c>    * --------------</c>
<c>    * END Code Block</c>
<c>    * --------------</c>
<l>    gen_image_const(ImageResult, 'byte', Width, Height)</l>
<l>    paint_region(Region, ImageResult, ImageResult, 255, 'fill')</l>
<l>    write_image(ImageResult, 'png', 0, out_img_path)</l>
<c></c>
<l>    binary_threshold(Label, LabelRegion, 'max_separability', 'light', UsedThreshold)</l>
<c>    </c>
<c>    * -- Compute MCC between predicted region and GT region for one image</c>
<c>    * mcc_region := procedure (PredRegion, GTRegion, Width, Height : MCC :) </c>
<c>    </c>
<c>    * Intersection (TP)</c>
<l>    intersection (Region, LabelRegion, R_tp)</l>
<l>    union1(R_tp, R_tp)</l>
<l>    area_center (R_tp, TP, Row, Col)</l>
<c>    </c>
<c>    * FP = Pred minus GT</c>
<l>    difference (Region, LabelRegion, R_fp)</l>
<l>    union1(R_fp, R_fp)</l>
<l>    area_center (R_fp, FP, Row, Col)</l>
<c>    </c>
<c>    * FN = GT minus Pred</c>
<l>    difference (LabelRegion, Region, R_fn)</l>
<l>    union1(R_fn, R_fn)</l>
<l>    area_center (R_fn, FN, Row, Col)</l>
<c>    </c>
<c>    * TN = total - (TP+FP+FN)</c>
<l>    Total := Width * Height</l>
<l>    TN := max2(0, Total - (TP + FP + FN))</l>
<c>    </c>
<l>    tuple_add(TP, FP, TP_plus_FP)</l>
<l>    tuple_add(TP, FN, TP_plus_FN)</l>
<l>    tuple_add(TN, FP, TN_plus_FP)</l>
<l>    tuple_add(TN, FN, TN_plus_FN)</l>
<c>    </c>
<l>    tuple_real(TN_plus_FN, TN_plus_FN)</l>
<l>    tuple_real(TP_plus_FN, TP_plus_FN)</l>
<l>    tuple_real(TN_plus_FP, TN_plus_FP)</l>
<l>    tuple_real(TN_plus_FN, TN_plus_FN)</l>
<c>    </c>
<l>    tuple_mult(TP_plus_FP, TP_plus_FN, Prod)</l>
<l>    tuple_mult(Prod, TN_plus_FP, Prod)</l>
<l>    tuple_mult(Prod, TN_plus_FN, Prod)</l>
<l>*    Den := sqrt((TP+FP) * (TP+FN) * (TN+FP) * (TN+FN))</l>
<c>    </c>
<l>    tuple_sqrt(Prod, Den)</l>
<c>    </c>
<l>    ImageMCC := 0.0</l>
<l>    if (Den &lt;= 0)</l>
<l>        ImageMCC := 0.0</l>
<l>    else</l>
<l>        *MCC := ((TP * TN) - (FP * FN)) / Den</l>
<l>        tuple_mult(TP, TN, TP_mult_TN)</l>
<l>        tuple_mult(FP, FN, FP_mult_FN)</l>
<c>            </c>
<l>        numerator := TP_mult_TN - FP_mult_FN</l>
<l>        if(Den &gt; 0.0)</l>
<l>            ImageMCC := ImageMCC + (numerator / Den)</l>
<l>        endif</l>
<l>        ImageMCC := ImageMCC / |TP_mult_TN|</l>
<l>    endif</l>
<c></c>
<l>    TotalMCC := TotalMCC + ImageMCC</l>
<l>    dev_display(Region)</l>
<l>    dev_disp_text(ImageMCC, 'window', 10, 10, 'red', [], [])</l>
<l>    dev_disp_text(TotalMCC, 'window', 30, 10, 'black', [], [])</l>
<l>    *wait_seconds(0.5)</l>
<l>endfor</l>
<c></c>
<l>MCC := TotalMCC / |ImageFiles|</l>
<l>wait_seconds(0.5)</l>
<l>dev_disp_text(MCC, 'window', 30, 10, 'black', [], [])</l>
<c></c>
<l>*exit()</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
